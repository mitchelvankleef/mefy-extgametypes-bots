/*
================================================================================
Utility Library
for Medal Of Honor
Version 1.4.2 (05/14/05)

Copyright (c) 2003-2005 Mark Follett
email: mef123@geocities.com

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is furnished
to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
================================================================================
*/


init_util_lib:
	level.mef_devmode = bool (getcvar "g_mef_devmode")
	waitthread get_version_info
	waitthread clear_playernames

	level.mef_version = "1.2.2"
	setcvar "g_mef_version" level.mef_version
	
	level.mef_settings[0] = NIL
	level.ctf_settings = level.mef_settings
	level.ft_settings = level.mef_settings
	level.dem_settings = level.mef_settings

	waitthread init_teams

	level.mef_stringconv["tolower"]          = waitthread string_makeconv "ABCDEFGHIJKLMNOPQRSTUVWXYZ"   "abcdefghijklmnopqrstuvwxyz"
	level.mef_stringconv["normalizemapname"] = waitthread string_makeconv "\\ABCDEFGHIJKLMNOPQRSTUVWXYZ" "/abcdefghijklmnopqrstuvwxyz"

	waitthread get_mapname
	thread global/libmef/hud.scr::hud_thread
	thread round_timer
end


init_teams:
	if (level.mef_allies != NIL)
	{
		end
	}

	level.mef_allies = waitthread init_teamdata "allies"
	level.mef_axis = waitthread init_teamdata "axis"
	level.mef_ffa = waitthread init_teamdata "ffa"
	level.mef_allies.otherteam = level.mef_axis
	level.mef_axis.otherteam = level.mef_allies
	level.mef_team["allies"] = level.mef_allies
	level.mef_team["axis"] = level.mef_axis
	level.mef_team["ffa"] = level.mef_ffa
end


init_teamdata local.team:
	local.ent = local createListener
	local.ent.name = local.team
	local.ent.numplayers = 0
	local.ent.totplayers = 0
	
	if (local.team == "allies")
	{
		local.ent.desc = "Allied"
	} else
	{
		local.ent.desc = "Axis"
	}
	
	local.ent.spawns[0] = NIL
	local.ent.spawnregions["all"][0] = NIL
end local.ent


get_version_info:
	local.gameversions[0] = "aademo"::"Medal of Honor Allied Assault 1.00 win-x86 Dec 12 2001"::"aa"
	local.gameversions[1] = "shdemo"::"Medal of Honor Spearhead Demo"::"sh"
	local.gameversions[2] = "btdemo"::"Medal of Honor: Breakthrough Demo"::"bt"
	local.gameversions[3] = "aa"::"Medal of Honor Allied Assault"::"aa"
	local.gameversions[4] = "sh"::"Medal of Honor Spearhead"::"sh"
	local.gameversions[5] = "bt"::"Medal of Honor: Breakthrough"::"bt"

	local.verinfo = waitthread parse_game_version local.gameversions (getcvar "version")
	level.mef_gameversion = local.verinfo[1]
	level.mef_versionnum = local.verinfo[2]
	level.mef_baseversion = local.verinfo[3]
end


get_gametype:
	waitthread init_util_lib
end (waitthread global/libmef/gametypes.scr::get_gametype)


read_gametype_settings:
end (waitthread global/libmef/gametypes.scr::read_gametype_settings)


waittill_roundstart:
	if (level.roundbased && !level.mef_devmode)
	{
		if (level.mef_roundstarted == NIL)
		{
			level.mef_roundstarted = 0
			thread roundstart_thread
		}
		
		while (!level.mef_roundstarted)
		{
			waitframe
		}
	}
end


roundstart_thread:
	while (1)
	{
		local.allies = 0
		local.axis = 0
		
		for (local.i = 1; local.i < ($player.size + 1); local.i++)
		{
			local.player = $player[local.i]
			
			if (local.player != NIL && local.player != NULL)
			{
				if (local.player.dmteam == "allies")
				{
					local.allies = 1
				} else if (local.player.dmteam == "axis")
				{
					local.axis = 1
				}
			}
		}
		
		if (local.allies && local.axis)
		{
			level.mef_roundstarted = 1
			end
		}
		
		waitframe
	}
end


round_timer:
	level.mef_elapsedtime = 0.0
	
	level waittill prespawn

	level waittill spawn
	
	waitthread global/libmef/util.scr::waittill_roundstart

	local.starttime = level.time
	while (1)
	{
		level.mef_elapsedtime = level.time - local.starttime
		wait 0.1
	}
end


get_mapname:
	level.mef_fullmapname = waitthread string_convert (getcvar "mapname") level.mef_stringconv["normalizemapname"]
	local.items = waitthread split_string level.mef_fullmapname "/"
	if (local.items.size > 0)
	{
		level.mef_mapname = local.items[(local.items.size - 1)]
		local.items[(local.items.size - 1)] = NIL
		level.mef_mapprefix = waitthread join_string local.items "/"
	} else
	{
		level.mef_mapname = ""
		level.mef_mapprefix = ""
	}
	
	if (level.mef_mapprefix == "obj" && level.mef_baseversion == "aa")
	{
		level.mef_maptype = "obj"
	} else
	{
		level.mef_maptype = "dm"
	}
	
	if (level.mef_defaultgametype == NIL)
	{
		if (level.mef_mapprefix == "obj")
		{
			local.towtest = waitthread split_string level.mef_mapname "_"
			if (local.towtest.size > 1 && local.towtest[(local.towtest.size - 1)] == "tow")
			{
				level.mef_defaultgametype = "fttow"
			} else
			{
				level.mef_defaultgametype = "ftobj"
			}
		} else
		{
			level.mef_defaultgametype = "ft"
		}
	}
end
	

parse_game_version local.gameversions local.verstring:
	for (local.i = 0; local.i < local.gameversions.size; local.i++)
	{
		local.version = local.gameversions[local.i]

		local.match = 1
		for (local.j = 0; local.j < local.version[2].size; local.j++)
		{
			if (local.j >= local.verstring.size || local.version[2][local.j] != local.verstring[local.j])
			{
				local.match = 0
				break
			}
		}

		if (local.match)
		{
			local.selversion = local.version
			break
		}
	}

	if (local.selversion != NIL)
	{
		local.vernum = ""
		for (local.j = local.selversion[2].size; (local.j < local.verstring.size && local.verstring[local.j] == " "); local.j++) {}
		for (; (local.j < local.verstring.size && local.verstring[local.j] != " "); local.j++)
		{
			local.vernum += local.verstring[local.j]
		}

		end local.selversion[1]::local.vernum::local.selversion[3]
	} else
	{
		end "aa"::"1.11"::"aa"
	}
end


string_makeconv local.fromchars local.tochars:
	for (local.i = 0; local.i < local.fromchars.size; local.i++)
	{
		local.map[(string local.fromchars[local.i])] = local.tochars[local.i]
	}
end local.map


string_equal local.stra local.strb local.map:
	if (local.stra.size != local.strb.size)
	{
		end 0
	}

	for (local.i = 0; local.i < local.stra.size; local.i++)
	{
		local.chara = local.stra[local.i]
		local.charb = local.strb[local.i]

		if (local.chara != local.charb && local.map[(string local.chara)] != local.charb && local.map[(string local.charb)] != local.chara)
		{
			end 0
		}
	}
end 1


string_tolower local.str:
end (waitthread string_convert local.str level.mef_stringconv["tolower"])


string_convert local.str local.map:
	local.result = ""
	for (local.i = 0; local.i < local.str.size; local.i++)
	{
		local.char = (string local.str[local.i])
		
		if (local.map[local.char] != NIL)
		{
			local.result += local.map[local.char]
		} else
		{
			local.result += local.str[local.i]
		}
	}
end local.result


make_sep_array local.seps:
	for (local.i = 0; local.i < local.seps.size; local.i++)
	{
		local.result[(string local.seps[local.i])] = 1
	}
end local.result


escape_string local.str local.schars local.escchar:
	local.result = ""

	local.carray = waitthread make_sep_array local.schars
	
	for (local.i = 0; local.i < local.str.size; local.i++)
	{
		local.char = string local.str[local.i]
		if (local.carray[local.char] || (local.char == local.escchar))
		{
			local.result += local.escchar
		}
		
		local.result += local.char
	}
end local.result


split_string local.str local.seps local.keepseps local.singlesep local.escchar:
	local.result[0] = NIL
	
	local.separray = waitthread make_sep_array local.seps

	local.i = 0
	local.escape = 0

	while (1)
	{
		local.item = ""
		while (local.i < local.str.size && (local.escape || !local.separray[(string local.str[local.i])]))
		{
			local.char = local.str[local.i]
			if (!local.escape && (local.char == local.escchar))
			{
				local.escape = 1
			} else
			{
				local.item += local.char
				local.escape = 0
			}
			local.i++
		}

		if (local.item != "" || local.keepseps)
		{
			local.result[local.result.size] = local.item
		}
	
		if (local.i == local.str.size)
		{
			end local.result
		}

		local.item = ""
		while (local.i < local.str.size && local.separray[(string local.str[local.i])])
		{
			local.item += local.str[local.i]
			local.i++
			
			if (local.singlesep)
			{
				break
			}
		}
		
		if (local.keepseps)
		{
			local.result[local.result.size] = local.item
		}
	}
end


string_popitem local.str local.seps:
	local.separray = waitthread make_sep_array local.seps

	local.result[0] = NIL
	local.i = 0
	
	while (local.i < local.str.size && local.separray[(string local.str[local.i])])
	{
		local.i++
	}
	
	local.result[0] = ""
	while (local.i < local.str.size && !local.separray[(string local.str[local.i])])
	{
		local.result[0] += local.str[local.i]
		local.i++
	}

	while (local.i < local.str.size && local.separray[(string local.str[local.i])])
	{
		local.i++
	}
	
	local.result[1] = ""
	while (local.i < local.str.size)
	{
		local.result[1] += local.str[local.i]
		local.i++
	}
end local.result


string_popkey local.str local.seps:
	local.separray = waitthread make_sep_array local.seps

	local.result[0] = NIL
	local.i = 0
	
	local.result[0] = ""
	while (local.i < local.str.size && !local.separray[(string local.str[local.i])])
	{
		local.result[0] += local.str[local.i]
		local.i++
	}
	
	if (local.i == local.str.size || local.result[0] == "")
	{
		end
	}

	while (local.i < local.str.size && local.separray[(string local.str[local.i])])
	{
		local.i++
	}
	
	local.result[1] = ""
	while (local.i < local.str.size)
	{
		local.result[1] += local.str[local.i]
		local.i++
	}
end local.result


read_chained_cvar local.name local.escapechar local.replace:
	local.maxcvars = 999
	local.i = 0
	local.result = getcvar local.name
	if (local.result == "")
	{
		end local.result
	}
	
	while (local.result[(local.result.size - 1)] == local.escapechar && local.i < local.maxcvars)
	{
		local.result[(local.result.size - 1)] = local.replace
		local.i++
		local.result += (getcvar (local.name + local.i))
	}
end local.result


join_string local.strlist local.sep:
	local.result = ""
	for (local.i = 0; local.i < local.strlist.size; local.i++)
	{
		if (local.i != 0)
		{
			local.result += local.sep
		}
		local.result += local.strlist[local.i]
	}
end local.result


read_position_string local.str local.ent:
	local.strlist = waitthread split_string local.str " "
	for (local.i = 0; local.i < 4; local.i++)
	{
		if (local.strlist[local.i] == NIL)
		{
			local.strlist[local.i] = 0.0
		} else
		{
			local.strlist[local.i] = (float(local.strlist[local.i]))
		}
	}
	
	local.poslist[0] = NIL
	for (local.i = 4; local.i < local.strlist.size; local.i++)
	{
		local.poslist[local.poslist.size] = local.strlist[local.i]
	}
	
	local.ent.origin = ( (float(local.strlist[0])) (float(local.strlist[1])) (float(local.strlist[2]) ) )
	local.ent.angles = ( 0 (float(local.strlist[3])) 0 )
	local.ent.desc = waitthread join_string local.poslist " "
	
	if (local.ent.desc == "")
	{
		local.ent.desc = waitthread global/libmef/mapdesc.scr::get_player_position local.ent.origin
	}
end local.ent


is_in_array local.array local.item local.static:
	if (local.static)
	{
		local.startidx = 1
		local.s = local.array.size + 1
	} else
	{
		local.startidx = 0
		local.s = local.array.size
	}

	for (local.i = local.startidx; local.i < local.s; local.i++)
	{
		if (local.array[local.i] == local.item)
		{
			end 1
		}
	}
end 0


append_arrays local.a1 local.a2:
	for (local.i = 0; local.i < local.a2.size; local.i++)
	{
		local.a1[local.a1.size] = local.a2[local.i]
	}
end


// entity broadcasting
auto_broadcast_ents local.delay:
	level.mef_broadcastents[0] = NIL
	
	while (1)
	{
		if (level.mef_broadcastents.size > 0)
		{
			local.ent = level.mef_broadcastents[0]
			if (local.ent != NULL && !local.ent.dontbroadcast)
			{
				local.ent svflags "+broadcast"
			}

			local.newents = NIL
			local.newents[0] = NIL
			for (local.i = 1; local.i < level.mef_broadcastents.size; local.i++)
			{
				local.newents[local.newents.size] = level.mef_broadcastents[local.i]
			}

			level.mef_broadcastents = local.newents
		}

		wait local.delay
	}
end


player_can_see local.player local.obj local.dist:
	local.eye = (local.player gettagposition "eyes bone")
	local.eyedir = (angles_toforward local.player.viewangles)
	local.dir = local.obj.origin - (local.eye - (local.eyedir * 30))
	local.dp = vector_dot (vector_normalize local.dir) (vector_normalize local.eyedir)

	if ((vector_length (local.obj.origin - (trace (local.player.origin + ( 0 0 60 )) local.obj.origin 1))) > 3.0)
	{
		end 0
	}

	if (((vector_length local.dir) - 30) > local.dist ||
	    local.dp < 0.5)
	{
		end 0
	}
end 1


get_player_name local.player:
	if (local.player == NIL || local.player == NULL)
	{
		end
	}

	if (local.player.netname != NIL)
	{
		if (local.player.netname != "")
		{
			end local.player.netname
		} else
		{
			end
		}
	}

	if (level.mef_playernameupdatetime == NIL || level.mef_playernameupdatetime <= level.time)
	{
		waitthread parse_foresight_names
		level.mef_playernameupdatetime = level.time + 2.0
	}

	local.name = level.mef_playernames[local.player.entnum]
	if (local.name != NIL && local.name != "")
	{
		end local.name
	}
end


parse_foresight_names:
	local.data = (getcvar "FST_playerNames")
	if (local.data == level.mef_playernamestring)
	{
		end
	}
	
	level.mef_playernamestring = local.data
	local.names = waitthread split_string local.data ";" 1 1 "&"

	level.mef_playernames = NIL
	level.mef_playernames[0] = NIL

	for (local.i = 0; local.i < local.names.size; local.i = local.i + 2)
	{
		level.mef_playernames[level.mef_playernames.size] = local.names[local.i]
	}
end


clear_playernames:
	if (level.mef_baseversion != "bt")
	{
		setcvar "FST_playerNames" ""
	}
end


get_map_floor_vals local.mapname:
	switch (local.mapname)
	{
		case "dm/mohdm2":
			local.botz = -128.125
			local.topz = -95.875
			break
				
		case "obj/obj_team1":
			local.botz = -520.125
			local.topz = -511.875
			break
				
		case "obj/obj_team3":
			local.botz = -728.125
			local.topz = -719.875
			break
		
		case "dm/mohdm3":
			local.botz = -224.125
			local.topz = -215.875
			break

		case "dm/mohdm4":
		case "obj/obj_team4":
			local.botz = -16.125
			local.topz = 0.125
			break
			
		case "dm/mp_unterseite_dm":
			local.botz = -656.125
			local.topz = -639.875
			break
			
		case "lib/mp_ship_lib":
			local.botz = -576.125
			local.topz = -511.875
			break
			
		default:
			end
			break
	}
end (local.botz::local.topz)


under_map_thread local.delay local.botz local.topz:
	local.idx = 0

	while (1)
	{
		if ($player.size > 0)
		{
			local.idx++
		
			if (local.idx > $player.size)
			{
				local.idx = 1
			}
		
			local.player = $player[local.idx]
			if (local.player != NIL && local.player != NULL && local.player.mef_pstate == "PSTATE_ALIVE" && IsAlive(local.player))
			{
				if (waitthread is_under_map local.player local.botz local.topz)
				{
					local.player takedamage
					local.player hurt 1000
					local.player iprint "You were killed for going under the map."
				}
			}
		}
		
		wait local.delay
	}
end


is_under_map local.player local.botz local.topz:
	local.zlow = -8000
	local.zhigh = 8000

	local.orglow = local.player.origin
	local.orglow[2] = local.zlow
		
	local.orghigh = local.player.origin
	local.orghigh[2] = local.zhigh
		
	local.down = trace (local.player.origin + ( 0 0 10 )) local.orglow 1 ( -5 -5 0 ) ( 5 5 0 )
	local.up = trace local.orglow local.orghigh

//	println ("BOTZ: " + local.up[2] + " TOPZ: " + local.down[2])

	if (((abs (local.down[2] - local.topz)) < 0.001) && ((abs (local.up[2] - local.botz)) < 0.001))
	{
		end 1
	}
end 0


add_logging_settings:
	if (!level.mef_loggingevents)
	{
		waitthread global/libmef/gametypes.scr::new_setting "logevents" bool 0
		level.mef_loggingevents = 1
	}
end


// Round Based Scoring System


begin_round_based_game local.resetthread local.teamwinthread:
	level.mef_rbdata = spawn script_origin
	level.mef_rbdata.origin = ( 0 0 0 )
	level.mef_rbdata.resetthread = local.resetthread
	level.mef_rbdata.teamwinthread = local.teamwinthread

	level.mef_score["allies"] = 0
	level.mef_score["axis"] = 0

	waitthread global/libmef/hud.scr::register_hud_item 1 global/libmef/util.scr::setup_score_display global/libmef/util.scr::update_score_display

	if (level.mef_rbdata.resetthread)
	{
		waitthread level.mef_rbdata.resetthread
	}

	waitthread turn_off_clock

	if (level.mef_settings["suddendeath"] > 0)
	{
		local.sdmins = level.mef_settings["suddendeath"]
	} else
	{
		local.sdmins = 0
	}

	if (level.mef_settings["fsuddendeath"] > 0)
	{
		local.fsdmins = level.mef_settings["fsuddendeath"]
	} else
	{
		local.fsdmins = 0
	}

	thread check_time local.sdmins local.fsdmins
end


turn_off_clock:
	level.clockside = "draw"
	level.defusing_team = "axis"
	level.planting_team = "allies"
	level.bombs_planted = 1
	level.bomb_set = 1
end


turn_on_clock local.clockside:
	level.clockside = local.clockside
	level.bombs_planted = 0
	level.bomb_set = 0
end


check_time local.sdmins local.fsdmins:
	local.starttime = level.time

	if (local.sdmins == 0 && local.fsdmins > 0)
	{
		local.fsuddendeath = 1
	}

	while (!level.mef_gameover)
	{
		local.elapsedtime = level.time - local.starttime

		if (local.elapsedtime >= ((level.dmroundlimit * 60) - 1) && !level.mef_scoring["allies"] && !level.mef_scoring["axis"])
		{
			if (level.mef_score["allies"] == level.mef_score["axis"])
			{
				if ((local.sdmins + local.fsdmins) > 0)
				{
					thread sudden_death local.sdmins local.fsdmins
				} else
				{
					waitthread do_teamwin draw
				}
			} else if (level.mef_score["allies"] > level.mef_score["axis"])
			{
				waitthread do_teamwin allies
			} else
			{
				waitthread do_teamwin axis
			}
			end
		} else if (local.fsuddendeath && !local.announcefsd && local.elapsedtime >= ((level.dmroundlimit * 60) - 20))
		{
			local.announcefsd = 1
			iprintlnbold_noloc "Warning: Respawning ends in 20 seconds."
		}

		wait 1
	}
end


sudden_death local.sdmins local.fsdmins:
	local.totalmins = local.sdmins + local.fsdmins

	level.mef_suddendeath = 1
	
	if (local.sdmins > 0)
	{
		iprintlnbold_noloc (local.sdmins + " Minute Sudden Death: Next score wins the round.")
	}
	level.mef_updatehud = 1

	local.endtime = level.time + (local.totalmins * 60)
	local.fsdtime = level.time + (local.sdmins * 60)
	
	if (local.fsdmins > 0)
	{
		local.autonuke = 1
	}
	
	while (!level.mef_gameover)
	{
		if (level.time >= local.endtime)
		{
			level.mef_timeexpired = 1
			if (local.autonuke)
			{
				level.mef_autonuke = 1
			}
			end
		} else if (local.fsdmins > 0 && !level.mef_fsuddendeath && level.time >= local.fsdtime)
		{
			level.mef_fsuddendeath = 1			
			iprintlnbold_noloc (local.fsdmins + " Minute Final Sudden Death: Respawning disabled.")
			waitthread global/libmef/respawn.scr::disable_respawn
		} else if (local.fsdmins > 0 && !level.mef_fsuddendeath && !local.announcefsd && level.time >= (local.fsdtime - 20))
		{
			local.announcefsd = 1
			iprintlnbold_noloc "Warning: Respawning ends in 20 seconds."
		} else if (local.autonuke && !local.announcenuke && level.time >= (local.endtime - 20))
		{
			local.announcenuke = 1
			thread global/libmef/respawn.scr::teamnuke_thread 20
		}

		wait 1
	}
end


get_winner_score:
	if (!level.mef_suddendeath)
	{
		end
	}

	local.rbwinner = waitthread global/libmef/respawn.scr::get_winner_rb
	
	if (local.rbwinner == NIL && level.mef_timeexpired)
	{
		local.rbwinner = "draw"
	}

	if (local.rbwinner == "draw")
	{
		if (level.mef_scoring["allies"] && level.mef_scoring["axis"])
		{
			// both are scoring, wait till it completes
			local.announcescoring = 1
		} else if (level.mef_scoring["allies"])
		{
			local.winner = "allies"
		} else if (level.mef_scoring["axis"])
		{
			local.winner = "axis"
		} else
		{
			local.winner = "draw"
		}
	} else if (local.rbwinner == "allies")
	{
		if (level.mef_scoring["axis"])
		{
			local.announcescoring = 1
		} else
		{
			local.winner = "allies"
		}
	} else if (local.rbwinner == "axis")
	{
		if (level.mef_scoring["allies"])
		{
			local.announcescoring = 1
		} else
		{
			local.winner = "axis"
		}
	}

	if (local.announcescoring && !level.mef_announcedscoring)
	{
		iprintlnbold_noloc "A score is in progress!"
		level.mef_announcedscoring = 1
	}
end local.winner


team_scored local.team:
	if (level.mef_gameover)
	{
		end
	}

	level.mef_score[local.team]++
	level.mef_updatehud = 1

	local.otherteam = waitthread get_other_team local.team

	if ((waitthread check_win local.team) && ((level.mef_suddendeath == 1) || !level.mef_scoring[local.otherteam]))
	{
		waitthread do_teamwin local.team
		end 1
	}

	level.mef_scoring[local.team] = NIL

	if (local.team == "allies")
	{
		local.str = "Allies score!"
		level.mef_rbdata playsound ("dfr_victory_v0" + string(randomint(3) + 1))
	} else
	{
		local.str = "Axis scores!"
		level.mef_rbdata playsound ("den_victory_v0" + string(randomint(3) + 1))
	}

	thread global/message_print.scr::message local.str

	if (level.mef_scoring[local.otherteam])
	{
		thread check_win_later local.team
	}

	if (level.mef_rbdata.resetthread)
	{
		waitthread level.mef_rbdata.resetthread
	}

	end 0
end


check_win_later local.team:
	local.otherteam = waitthread get_other_team local.team

	while (level.mef_scoring[local.otherteam] && !level.mef_suddendeath)
	{
		waitframe
	}

	if ((waitthread check_win local.team))
	{
		waitthread do_teamwin local.team
	}
end


check_win local.team:
	local.otherteam = waitthread get_other_team local.team

	if ((level.mef_score[local.team] > level.mef_score[local.otherteam]) && \
	    (level.mef_suddendeath || \
	    ((level.mef_settings["pointlimit"] > 0) && (level.mef_score[local.team] >= level.mef_settings["pointlimit"]))))
	{
		end 1
	}
end 0


get_other_team local.team:
	if (local.team == "allies")
	{
		end "axis"
	} else if (local.team == "axis")
	{
		end "allies"
	} else
	{
		end "both"
	}
end


parse_clockside local.clockside:
	switch (local.clockside)
	{
		case 255:
		case "allies":
			end "allies"
			break
			
		case 256:
		case "axis":
			end "axis"
			break
		
		case 257:
		case "draw":
			end "draw"
			break
		
		case 258:
		case "kills":
			end "kills"
			break
			
		default:
			end "draw"
			break
	}
end
			
		
do_teamwin local.team:
	if (level.mef_basegametype == NIL)
	{
		teamwin local.team
		end
	}

	level.mef_gameover = 1

	waitthread display_credits level.mef_credits

	if (level.mef_rbdata != NIL && level.mef_rbdata.teamwinthread != NIL)
	{
		waitthread level.mef_rbdata.teamwinthread local.team
	}

	if (local.team == "allies" || local.team == "axis")
	{
		waitthread teamwin_wrapper local.team
	} else
	{
		waitthread force_draw
	}

	thread hide_hud_delay
end


teamwin_wrapper local.team:
	if (level.mef_basegametype != 4 || level.mef_basegametype != 5 || level.mef_basegametype != 6)
	{
		setcvar "g_gametype" "4"  // Objective Match
	}

	teamwin local.team

	setcvar "g_gametype" level.mef_basegametype
end


force_draw:
	if (level.mef_baseversion == "bt")
	{
		waitthread teamwin_wrapper none
		end
	}

	level.clockside = "draw"
	level.bombs_planted = 0
	level.bomb_set = 0
	level.targets_to_destroy = 1
	level.targets_destroyed = 0
	if (level.mef_gametype == "fttow")
	{
		level commanddelay 0 ignoreclock 0
	}

	if (level.mef_elapsedtime < ((level.dmroundlimit * 60) - 1))
	{
		if (level.mef_elapsedtime < 62)
		{
			// kill all players
			level.mef_disablespectating = 1
			waitframe
			waitframe
			
			level.dmrespawning = 0
			waitthread global/libmef/respawn.scr::disable_respawn

			for (local.i = 1; local.i < ($player.size + 1); local.i++)
			{
				local.player = $player[local.i]
				if (local.player != NIL && local.player != NULL)
				{
					local.player takedamage
					local.player hurt 1000
				}
			}
		} else
		{
			// temp set roundlimit to 1
			local.roundlimit = int (getcvar "roundlimit")
			setcvar "roundlimit" "1"
			waitframe
			setcvar "roundlimit" local.roundlimit
		}
	} else
	{
		// wait for the clock to expire so the hud turns off at the precise time
		local.waittime = (level.dmroundlimit * 60) - level.mef_elapsedtime
		if (local.waittime > 0)
		{
			wait local.waittime
		}
	}
	
	thread force_draw_failsafe
end


force_draw_failsafe:
	wait 8
	
	// failsafe to force the draw if the other methods didnt work
	local.manager = spawn DM_Manager
	local.manager doroundtransition
end


hide_hud_delay:
	wait 1.75
	
	if (level.mef_settings["observe_builtin"])
	{
		level.mef_disablespectating = 1
	}

	wait 0.25
	
	// turn off the center message
	thread global/message_print.scr::message ""
	huddraw_alpha 26 0
	huddraw_alpha 27 0
	level.mef_hidehud = 1
end


display_credits local.credits:
	if (local.credits == NIL)
	{
		end
	}

	for (local.i = 0; local.i < 6; local.i++)
	{
		if (local.credits[local.i] == NIL)
		{
			local.credits[local.i] = ""
		}
	}

	setcvar "g_obj_alliedtext1" local.credits[0]
	setcvar "g_obj_alliedtext2" local.credits[1]
	setcvar "g_obj_alliedtext3" local.credits[2]
	setcvar "g_obj_axistext1"   local.credits[3]
	setcvar "g_obj_axistext2"   local.credits[4]
	setcvar "g_obj_axistext3"   local.credits[5]
	
	if (level.mef_basegametype == 5)
	{
		setcvar "g_obj_alliedtext4" ""
		setcvar "g_obj_alliedtext5" ""
		setcvar "g_obj_axistext4" ""
		setcvar "g_obj_axistext5" ""
	}
end


setup_score_display:
	local.coordx = -163
	local.coordy = 70

	huddraw_align  self.idx right top
	huddraw_font   self.idx facfont-20
	huddraw_rect   self.idx (local.coordx + 30) local.coordy 100 100
	huddraw_color  self.idx 1.0 1.0 1.0
	huddraw_alpha  self.idx 1.0
end


update_score_display:
	local.str = "Allies: " + level.mef_score["allies"] + " Axis: " + level.mef_score["axis"]

	if (level.mef_suddendeath == 1)
	{
		local.str += " [SD]"
	} else if (level.mef_settings["pointlimit"] > 0)
	{
		local.str += " [" + level.mef_settings["pointlimit"] + "]"
	}

	huddraw_string self.idx local.str
end
